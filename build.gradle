buildscript {
    ext.kotlin_version = '1.2.50'
    ext.slf4j_version = '1.7.25'
    ext.fuel_version = '1.12.1'
    ext.jackson_version = '2.9.5'

    ext.wiremock_version = '2.17.0'
    ext.spek_version = '1.1.5'
    ext.kluent_version = '1.37'
}

plugins {
    id 'org.jmailen.kotlinter' version '1.12.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id "org.jetbrains.dokka" version "0.9.17"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task packageJavadoc(type: Jar) {
    from dokka
    classifier = 'javadoc'
}

test {
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

group 'no.nav.common'
version '1.0.4'
sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-jackson:$fuel_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    testImplementation "org.jetbrains.spek:spek-api:$spek_version"
    testImplementation "org.jetbrains.spek:spek-data-driven-extension:$spek_version"
    testImplementation "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "com.github.tomakehurst:wiremock:$wiremock_version"
    testImplementation 'org.junit.platform:junit-platform-runner:1.2.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            credentials {
                username "$nexusUser"
                password "$nexusPassword"
            }
            url = "https://repo.adeo.no/repository/maven-${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
        }
    }
}