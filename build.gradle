buildscript {
    ext.kotlin_version = '1.2.70'
    ext.slf4j_version = '1.7.25'
    ext.fuel_version = '1.14.0'
    ext.jackson_version = '2.9.7'

    ext.wiremock_version = '2.19.0'
    ext.spek_version = '1.2.1'
    ext.kluent_version = '1.41'
}

plugins {
    id 'org.jmailen.kotlinter' version '1.18.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.2.70'
    id "org.jetbrains.dokka" version "0.9.17"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task packageJavadoc(type: Jar) {
    from dokka
    classifier = 'javadoc'
}

test {
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

group 'no.nav.common'
version '1.0.8'
sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
        "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
        "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version",
        "com.github.kittinunf.fuel:fuel:$fuel_version",
        "com.github.kittinunf.fuel:fuel-jackson:$fuel_version",
        "org.slf4j:slf4j-api:$slf4j_version"
    testImplementation "org.jetbrains.spek:spek-api:$spek_version",
        "org.jetbrains.spek:spek-data-driven-extension:$spek_version",
        "org.jetbrains.spek:spek-junit-platform-engine:$spek_version",
        "org.amshove.kluent:kluent:$kluent_version",
        "com.github.tomakehurst:wiremock:$wiremock_version",
        'org.junit.platform:junit-platform-runner:1.2.0'
    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
}

task printVersion() {
    doLast {
        println(version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            url = "https://repo.adeo.no/repository/maven-${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
        }
    }
}